apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'signing'

group = 'io.smartmachine'
version = '0.2.0'

configurations {
    provided
    testCompile.extendsFrom provided
}

repositories {
    mavenCentral()
}

dependencies {
    compile 'com.couchbase.client:couchbase-client:1.4.2'
    provided 'io.dropwizard:dropwizard-core:0.7.1'
    testCompile "junit:junit:4.11"
    testCompile "org.mockito:mockito-core:1.9.5"
    testCompile "io.dropwizard:dropwizard-testing:0.7.1"
}

test {

    // include 'io/smartmachine/dropwizard/couchbase/**'

    testLogging.showStandardStreams = true
    testLogging.showExceptions = true
    testLogging.showStackTraces = true
    testLogging.showCauses = true
    testLogging.setDisplayGranularity(-1)

    beforeTest { descriptor ->
        logger.lifecycle("Running test: " + descriptor)
    }

}

javadoc {
    classpath = configurations.compile + configurations.provided
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from "build/docs/javadoc"
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment)}
            //repository(url: "file:///$projectDir/artifacts")
            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: sonatypeUser, password: sonatypePassword)
            }

            pom.project {
                name 'dropwizard-couchbase'
                packaging 'jar'
                description 'Couchbase persistence module for dropwizard.'
                url 'http://smartmachine.io/dropwizard-couchbase'

                scm {
                    url 'scm:git@github.com:smartmachine/dropwizard-smartmachine.git'
                    connection 'scm:git@github.com:smartmachine/dropwizard-smartmachine.git'
                    developerConnection 'scm:git@github.com:smartmachine/dropwizard-smartmachine.git'
                }

                licenses {
                    license {
                        name 'MIT License'
                        url 'http://www.opensource.org/licenses/mit-license.php'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'knutster'
                        name 'Conrad Christensen'
                    }
                }
            }
        }
    }
}

sourceSets {
    main {
        compileClasspath = compileClasspath + configurations.provided
    }
}

plugins.withType(IdeaPlugin) {
    idea {
        module {
            scopes.PROVIDED.plus += configurations.provided
            scopes.COMPILE.minus += configurations.provided
            scopes.TEST.minus += configurations.provided
            scopes.RUNTIME.minus += configurations.provided
        }
    }
}
